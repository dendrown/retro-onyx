(*
 * ONYXLOGO
 * Onyx Technologies logo and more!
 *
 * Author: Dennis Drown
 * System: Apple II Pascal
 *)
(*$S+*)
PROGRAM OnyxLogo(INPUT, OUTPUT);
USES AppleStuff, Transcend, TurtleGraphics;

CONST
  NUMCIRCLES    = 4;
  LETTERHEIGHT  = 20;
  LETTERMARGIN  = 10;
  YMAX          = 191;          (* Max vert.  pixel index for full screen *)
  TEXTLINE      = 8;            (* Text line height in pixels *)

TYPE
  Quadrant  = (FULL, SE, NE, NW, SW);
  CircleDef = Record (* Tweak for best results since our turtle isn't
                        programmed do circles *)
                X, Y    : Integer;  (* Coords of first Arc at circle bottom *)
                ArcLen  : Integer;  (* Length of an "arc" to draw the circle *)
                ArcCnt  : Integer;  (* Num arcs in circle; use 360 multiple *)
                Gap     : Quadrant; (* Leave a gap in the specified quadrant *)
              End;
  Blueprint  = Array[1..NUMCIRCLES] of CircleDef;
  StatusLine = 1..2;

VAR
  Answer    : Char;             (* Input to see if user wishes to continue *)
  I         : Integer;
  Logo      : Blueprint;

(*
 * PrepDrawXY
 * Outputs a string in graphics mode
 *)
PROCEDURE PrepDrawXY(X, Y : Integer);
BEGIN
  PenColor(NONE);
  MoveTo(X, Y);
  TurnTo(0);
  PenColor(WHITE)
END; (* PrepDrawXY *)


(*
 * WriteXY:
 * Outputs a string in graphics mode
 *)
PROCEDURE WriteXY(X, Y : Integer; TxtMsg : String);
BEGIN
  PrepDrawXY(X, Y);
  WString(TxtMsg)
END; (* WriteXY *)


(*
 * Status:
 * Outputs a string in the status area (top of screen)
 *)
PROCEDURE Status(LineNum : StatusLine; TxtMsg : String);
VAR
  Y : Integer;

BEGIN
  Y := YMAX - (LineNum * TEXTLINE);
  WriteXY(0, Y, '                                        ');
  WriteXY(0, Y, TxtMsg)
END; (* Status *)


(*
 * MakeCircle:
 * Draws a circle. (X,Y) designates the bottom. ARC gives the length of each
 * arc drawn as the turtle moves on the perimeter. Bigger arc, bigger circle.
 *)
PROCEDURE MakeCircle(X, Y, ArcLen, ArcCnt : Integer; Gap : Quadrant);
VAR
  I, Theta  : Integer;

  (* Set or lift the turtle pen depending on the Gap placement *)
  PROCEDURE PenUpDown;
  VAR
    Up0, UpN : Integer;

  BEGIN
    If Gap = FULL Then
      PenColor(WHITE)
    Else
    Begin
      Up0 := Round((Ord(Gap)-1) / 4 * ArcCnt) + 2;
      UpN := Up0 + Round(ArcCnt / 10);

      If (I > Up0) AND (I < UpN) Then
        PenColor(NONE)
      Else
        PenColor(WHITE)
    End
  END;

BEGIN
  Theta := 360 DIV ArcCnt;

  PrepDrawXY(X, Y);
  For I := 1 to ArcCnt Do
  Begin
    PenUpDown;
    Move(ArcLen);
    Turn(Theta)
  End
END; (* MakeCircle *)


(*
 * MakeName:
 * Draws the stylized Onyx name starting at the specified (X,Y) position
 *)
PROCEDURE MakeName(X, Y : Integer);
CONST
  ARCLEN = 6;
  ARCCNT = 10;
  TRACKING = 16;

VAR
  Diagonal          : Integer;          (* For the N & X *)
  Xc, Yc, HalfHeight: Integer;          (* For the Y: (Xc,Yc) is the centre *)

BEGIN
  Diagonal :=  Round(Sqrt(TRACKING*TRACKING + LETTERHEIGHT*LETTERHEIGHT)) + 1;
  HalfHeight := LETTERHEIGHT DIV 2;

  (* Give me an O *)
  MakeCircle(X, Y, ARCLEN, ARCCNT, FULL);

  (* Give me an N *)
  X := X + TRACKING + 1;                (* Kern wider because the O is fat *)
  PrepDrawXY(X, Y);
  TurnTo(90);   Move(LETTERHEIGHT);
  TurnTo(-45);  Move(Diagonal);

  (* Give me an Y *)
  X := X + TRACKING;
  PrepDrawXY(X + (TRACKING DIV 2), Y);
  TurnTo(90);   Move(HalfHeight);
  Xc := TurtleX;
  Yc := TurtleY;
  Turn(45);     Move(Diagonal DIV 2);
  PrepDrawXY(Xc, Yc);
  TurnTo(45);   Move(Diagonal DIV 4);

  (* Give me an X *)
  X := X + TRACKING - 2;                (* Kern narrower for Y-X harmony *)
  PrepDrawXY(X, Y);
  TurnTo(45);   Move(Diagonal);
  PrepDrawXY(X, Y + LETTERHEIGHT);
  TurnTo(-45);  Move(Diagonal)
END; (* MakeName *)


(*
 * INITIALIZE
 * Initializes globals and sets up screen and
 *)
PROCEDURE Initialize;

  (* Use a larger arc count for larger circles, but always multiples of 360 *)
  PROCEDURE InitCircle(I, X0, Y0, Len, Cnt : Integer; Quad : Quadrant);
  BEGIN
    With Logo[I] Do
    Begin
      X := X0;          Y := Y0;
      ArcLen := Len;    ArcCnt := Cnt;
      Gap := Quad;
    End;
  END;

BEGIN
  Answer := 'Y';
  Randomize;

  (* Use a larger arc count for larger circles, but always multiples of 360 *)
  InitCircle(1, 139, 100,  6, 10, FULL);    (* Inner... *)
  InitCircle(2, 137,  87, 10, 15, SW);
  InitCircle(3, 134,  74, 16, 15, SE);
  InitCircle(4, 132,  54, 20, 18, NW);      (* ...outer *)

  InitTurtle;
  Status(1, 'Welcome to Onyx!');
  Status(2, 'We''re setting things up...')
END; (* Initialize *)


(*
 * MAIN: Displays the Onyx logo
 *)
BEGIN
  Initialize;
  While (Answer <> 'N') AND (Answer <>  'n') Do
  Begin
    For I := 1 TO NUMCIRCLES Do
      With Logo[I] Do
        MakeCircle(X, Y, ArcLen, ArcCnt, Gap);

    MakeName(110, Logo[NUMCIRCLES].Y - LETTERHEIGHT - LETTERMARGIN);
    Read(KEYBOARD, Answer)
  End;

  (* Say goodbye... *)
  TextMode;
  WriteLn;
  WriteLn('Thank you from Onyx Technologies!');
END.
