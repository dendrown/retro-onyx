(*
 * ONYXLOGO
 * Onyx Technologies logo and more!
 *
 * Author: Dennis Drown
 * System: Apple II Pascal
 *)
(*$S-*)
PROGRAM OnyxLogo(INPUT, OUTPUT);
USES AppleStuff, Transcend, TurtleGraphics;

CONST
  RADIUS0       = 8;
  DELTARADIUS   = 8;            (* Expand each circle this far *)
  NUMCIRCLES    = 4;
  XCIRCLE0      = 139;          (* Centre of circles: horizontal *)
  YCIRCLE0      = 110;          (* Centre of circles: vertical *)
  YMAX          = 191;          (* Max vert.  pixel index for full screen *)
  TEXTLINE      = 8;            (* Text line height in pixels *)

TYPE
  StatusLine    = 1..2;

VAR
  Answer    : Char;             (* Input to see if user wishes to continue *)
  I, Radius : Integer;

(*
 * PrepDrawXY
 * Outputs a string in graphics mode
 *)
PROCEDURE PrepDrawXY(X, Y : Integer);
BEGIN
  PenColor(NONE);
  MoveTo(X, Y);
  TurnTo(0);
  PenColor(WHITE)
END; (* PrepDrawXY *)


(*
 * WriteXY:
 * Outputs a string in graphics mode
 *)
PROCEDURE WriteXY(X, Y : Integer; TxtMsg : String);
BEGIN
  PrepDrawXY(X, Y);
  WString(TxtMsg)
END; (* WriteXY *)


(*
 * Status:
 * Outputs a string in the status area (top of screen)
 *)
PROCEDURE Status(LineNum : StatusLine; TxtMsg : String);
VAR
  Y : Integer;

BEGIN
  Y := YMAX - (LineNum * TEXTLINE);
  WriteXY(0, Y, '                                        ');
  WriteXY(0, Y, TxtMsg)
END; (* Status *)


(*
 * MakeCircle:
 * Draws a circle. (X,Y) designates the bottom. ARC gives the length of each
 * arc drawn as the turtle moves on the perimeter. Bigger arc, bigger circle.
 *)
PROCEDURE MakeCircle(X, Y, Arc : Integer);
CONST
  MINARCS = 10;

VAR
  I, Theta  : Integer;
  NumArcs   : Integer;

BEGIN
  NumArcs := 10; (*MINARCS + Trunc(Ln(Arc));*)
  Theta := 360 DIV NumArcs;

  PrepDrawXY(X, Y);
  For I := 1 to NumArcs Do
  Begin
    Move(Arc);
    Turn(Theta)
  End
END; (* MakeCircle *)


(*
 * CircleByRadius:
 * Draws a circle. (Xc,Yc) designates the centre.
 *)
PROCEDURE CircleByRadius(Xc, Yc, Radius : Integer);

VAR
  Arc, X, Y : Integer;

BEGIN
  Arc := Round(0.7 * Radius);
  Y := Yc - Radius -1;
  X := Xc - (Arc DIV 2) -1;

  MakeCircle(X, Y, Arc)
END; (* CircleByRadius *)


(*
 * MakeLogo:
 * Draws the Onyx Logo starting at the specified (X,Y) position
 *)
PROCEDURE MakeLogo(X, Y : Integer);
CONST
  ARCLEN = 6;
  HEIGHT = 20;
  TRACKING = 16;

VAR
  Diagonal          : Integer;          (* For the N & X *)
  Xc, Yc, HalfHeight: Integer;          (* For the Y: (Xc,Yc) is the centre *)

BEGIN
  Diagonal :=  Trunc(Sqrt(TRACKING*TRACKING + HEIGHT*HEIGHT)) + 2;  (* Two? *)
  HalfHeight := HEIGHT DIV 2;

  (* Give me an O *)
  MakeCircle(X, Y, ARCLEN);

  (* Give me an N *)
  X := X + TRACKING + 1;                (* Kern wider because the O is fat *)
  PrepDrawXY(X, Y);
  TurnTo(90);   Move(HEIGHT);
  TurnTo(-45);  Move(Diagonal);

  (* Give me an Y *)
  X := X + TRACKING;
  PrepDrawXY(X + (TRACKING DIV 2), Y);
  TurnTo(90);   Move(HalfHeight);
  Xc := TurtleX;
  Yc := TurtleY;
  Turn(45);     Move(Diagonal DIV 2);
  PrepDrawXY(Xc, Yc);
  TurnTo(45);   Move(Diagonal DIV 4);

  (* Give me an X *)
  X := X + TRACKING - 2;                (* Kern narrower for Y-X harmony *)
  PrepDrawXY(X, Y);
  TurnTo(45);   Move(Diagonal);
  PrepDrawXY(X, Y + HEIGHT);
  TurnTo(-45);  Move(Diagonal)
END; (* MakeLogo *)


(*
 * INITIALIZE
 * Initializes globals and sets up screen and
 *)
PROCEDURE Initialize;
BEGIN
  Answer := 'Y';
  Radius := RADIUS0;
  Randomize;

  InitTurtle;
  Status(1, 'Welcome to Onyx!');
  Status(2, 'We''re setting things up...')
END; (* Initialize *)


(*
 * MAIN: Displays the Onyx logo
 *)
BEGIN
  Initialize;
  While (Answer <> 'N') AND (Answer <>  'n') Do
  Begin
    For I := 1 TO NUMCIRCLES Do
    Begin
      CircleByRadius(XCIRCLE0, YCIRCLE0, Radius);
      Radius := Radius + DELTARADIUS;
    End;

    MakeLogo(110, 40);
    Read(KEYBOARD, Answer)
  End;

  (* Say goodbye... *)
  TextMode;
  WriteLn;
  WriteLn('Thank you from Onyx Technologies!');
END.
